programa parcial-1
areas
  areaCarrera: AreaPC(1,1,21,40)
  areaFiscal: AreaP(25,1,25,1)
robots
  robot jugador
  variables
    avIn,caIn,id,av,pasos,cont,calleAct: numero
    iniciar: boolean
  comenzar
    avIn:= PosAv
    caIn:= PosCa
    RecibirMensaje(id,robF)
    RecibirMensaje(av,robF)
    Pos(av,caIn)
    RecibirMensaje(iniciar,robF)
    repetir 5
      cont:= 0
      Random(pasos,5,10)
      mientras ((pasos<>cont) & (PosCa<40))
        mientras (HayPapelEnLaEsquina)
          tomarPapel
        mover
        cont:= cont + 1
    EnviarMensaje(id,robF)
    calleAct:= PosCa
    EnviarMensaje(calleAct,robF)
    si(HayPapelEnLaBolsa)
      BloquearEsquina(1,30)
      Pos(1,30)
      mientras(HayPapelEnLaBolsa)
        depositarPapel
      Pos(avIn,caIn)
      LiberarEsquina(1,30)
    sino
      Pos(avIn,caIn)
  fin
  robot fiscal
  variables
    av,id,ca,ganador,caMax: numero
  comenzar
    EnviarMensaje(1,jug1)
    EnviarMensaje(2,jug2)
    EnviarMensaje(3,jug3)
    
    Random(av,1,7)
    EnviarMensaje(av,jug1)
    Random(av,8,14)
    EnviarMensaje(av,jug2)
    Random(av,15,21)
    EnviarMensaje(av,jug3)
    
    EnviarMensaje(V,jug1)
    EnviarMensaje(V,jug2)
    EnviarMensaje(V,jug3)
    
    ganador:= 0
    caMax:= 0
    repetir 3
      RecibirMensaje(id,*)
      si(id = 1)
        RecibirMensaje(ca,jug1)
      sino
        si(id = 2)
          RecibirMensaje(ca,jug2)
        sino
          si(id = 3)
            RecibirMensaje(ca,jug3)
      si(ca > caMax)
        ganador:= id
        caMax:= ca
    Informar(ganador)
  fin
variables
  jug1,jug2,jug3: jugador
  robF: fiscal
comenzar
  AsignarArea(robF,areaFiscal)
  
  AsignarArea(jug1,areaCarrera)
  AsignarArea(jug2,areaCarrera)
  AsignarArea(jug3,areaCarrera)
  
  Iniciar(robF,25,1)
  Iniciar(jug1,1,1)
  Iniciar(jug2,8,1)
  Iniciar(jug3,15,1)
fin
